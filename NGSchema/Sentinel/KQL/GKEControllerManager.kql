source 
| where logName contains 'container.googleapis.com%2Fcontroller-manager' 
| extend 
    TimeGenerated = timestamp,
    InsertId = tostring(insertId),
    LogName = tostring(logName),
    Labels = todynamic(labels),
    Message = tostring(jsonPayload.message),
    Pid = tostring(jsonPayload.pid),
    Severity = tostring(severity),
    SourceFile = iff(isnotempty(todynamic(sourceLocation)), tostring(sourceLocation.file), ''),
    SourceLine = iff(isnotempty(todynamic(sourceLocation)), tostring(sourceLocation.line), ''),
    ReceiveTimestamp = receiveTimestamp,
    ClusterName = tostring(resource.labels.cluster_name),
    ComponentLocation = tostring(resource.labels.component_location),
    ComponentName = tostring(resource.labels.component_name),
    Location = tostring(resource.labels.location),
    ProjectID = tostring(resource.labels.project_id),
    ComputeResourceName = tostring(labels['compute.googleapis.com/resource_name'])
| extend 
    Logger = extract(@'logger=\\\"([^\\\"]+)\\\"', 1, Message),
    Kind = extract(@'kind=\\\"([^\\\"]+)\\\"', 1, Message),
    Key = extract(@'key=\\\"([^\\\"]+)\\\"', 1, Message),
    Duration = extract(@'duration=\\\"([^\\\"]+)\\\"', 1, Message),
    SyncStatus = iff(Message contains 'Finished syncing', 'Finished syncing', '')
| project 
    TimeGenerated,
    InsertId,
    LogName,
    Labels,
    Message,
    Pid,
    Severity,
    SourceFile,
    SourceLine,
    ReceiveTimestamp,
    ClusterName,
    ComponentLocation,
    ComponentName,
    Location,
    ProjectID,
    ComputeResourceName,
    Logger,
    Kind,
    Key,
    Duration,
    SyncStatus
